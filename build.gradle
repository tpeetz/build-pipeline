plugins {
    id 'org.asciidoctor.jvm.pdf' version '3.1.0'
    id 'org.asciidoctor.jvm.gems' version '3.1.0'
    id 'java'
    id 'groovy'
}

sourceCompatibility = 1.8
group 'com.ibtp.devops.pipeline'
description = 'Jenkins Pipeline Library'
version = '0.1.0-SNAPSHOT'

repositories {
    maven {
        url "http://repo.jenkins-ci.org/releases/"
    }
    mavenCentral()
    jcenter()
    ruby.gems()
}

dependencies {
    asciidoctorGems 'rubygems:rouge:3.15.0'
    compile 'org.slf4j:slf4j-api:1.7.22'
    compile 'org.codehaus.groovy:groovy-all:2.4.9'
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-all:1.9.5'
    testCompile 'com.lesfurets:jenkins-pipeline-unit:1.1'
    testCompile('org.spockframework:spock-core:1.1-groovy-2.4')
    //testCompile 'org.jenkins-ci.main:jenkins-core:2.73.3'
    testCompile 'org.jenkins-ci.main:jenkins-core:2.+'
}

final BUILD_DATE = new Date().format('dd.MM.yyyy').toString()
final DOK_VERSION = version

asciidoctorPdf {
    dependsOn asciidoctorGemsPrepare

    baseDirFollowsSourceFile()

    asciidoctorj {
        attributes  'build-gradle': file('build.gradle'),
                    'source-highlighter': 'rouge',
                    'revdate': BUILD_DATE,
                    'revnumber': DOK_VERSION,
                    'endpoint-url': 'http://www.ingenieurbuero-peetz.de',
                    'imagesdir': './images',
                    'chapter-label': '',
                    'icons': 'font',
                    'idprefix': 'id_',
                    'idseparator': '-',
                    'setanchors': '',
                    'docinfo1': ''
    }
}

tasks.withType(GroovyCompile) {
    options.compilerArgs << "-proc:none"
}

task copyLibrary(type: Copy) {
    from '.'
    into 'build/libs/build-pipeline@master'
    include 'src/**'
    include 'vars/**'
}

check.dependsOn(['copyLibrary'])
build.dependsOn(['asciidoctor'])

task asciidoctor(dependsOn: asciidoctorPdf)
build.dependsOn(['asciidoctor'])

wrapper {
    gradleVersion = "6.3"
}
